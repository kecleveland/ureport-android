apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('signature/keys.properties').newDataInputStream())

android {
    signingConfigs {
        signature {
            keyAlias localProperties.getProperty('signature.keyAlias')
            keyPassword localProperties.getProperty('signature.keyPassword')
            storeFile file(localProperties.getProperty('signature.storeFile'))
            storePassword localProperties.getProperty('signature.storePassword')
        }
    }
    lintOptions {
        disable 'MissingTranslation'
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "in.ureport"
        minSdkVersion 16
        targetSdkVersion 28
        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.signature
            buildConfigField 'boolean', 'SANDBOX_ORG', 'true'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['debug']
        }
        beta {
            minifyEnabled true
            debuggable false
            lintOptions { disable 'MissingTranslation' }
            buildConfigField 'boolean', 'SANDBOX_ORG', 'false'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.signature
            matchingFallbacks = ['release']
        }
        release {
            minifyEnabled true
            debuggable false
            lintOptions { disable 'MissingTranslation' }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.signature
            buildConfigField 'boolean', 'SANDBOX_ORG', 'false'
        }
    }

    flavorDimensions "common"

    productFlavors {
        regular {
            defaultConfig {
                versionCode 47
                versionName "2.8.5"
            }
        }
        onthemove {
            applicationIdSuffix ".onthemove"
            versionNameSuffix "-onthemove"
            defaultConfig {
                versionCode 1
                versionName "1.0.0"
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':support-android')
    implementation project(':video-compressor')

    implementation 'com.twitter.sdk.android:twitter:3.1.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }

    implementation 'com.google.android.gms:play-services-plus:16.0.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.android.gms:play-services-identity:16.0.0'
    implementation 'com.google.android.gms:play-services-analytics:16.0.6'

    implementation "com.android.support:design:${versions.androidSupportVersion}"
    implementation "com.android.support:appcompat-v7:${versions.androidSupportVersion}"
    implementation "com.android.support:recyclerview-v7:${versions.androidSupportVersion}"
    implementation "com.android.support:cardview-v7:${versions.androidSupportVersion}"
    implementation "com.android.support:preference-v7:${versions.androidSupportVersion}"
    implementation 'com.android.support:multidex:1.0.1'

    implementation 'com.github.marcorei:Infinite-Fire:2.0.0'
    implementation('com.github.ilhasoft.rapidpro-android-sdk:fcm-channel:1.0.4.2') {
        exclude group: 'com.android.support'
        exclude group: 'com.google.firebase'
    }

    implementation('com.google.firebase:firebase-database:16.0.2') {
        exclude group: 'com.android.support'
    }
    implementation 'com.google.firebase:firebase-auth:16.0.5'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation "com.facebook.android:facebook-android-sdk:${versions.facebookSdkVersion}"
    implementation "com.squareup.picasso:picasso:${versions.picassoVersion}"
    implementation "com.squareup.retrofit:retrofit:${versions.retrofitVersion}"

    implementation 'br.com.ilhasoft.support:support-view:1.0.3'
    implementation 'com.journeyapps:zxing-android-embedded:3.2.0@aar'
    implementation 'com.google.zxing:core:3.2.1'
    implementation 'com.moxun:tagcloudlib:1.1.3'
    implementation 'de.hdodenhof:circleimageview:1.3.0'
    implementation 'com.amazonaws:aws-android-sdk-s3:2.2.12'
    implementation 'fr.baloomba:viewpagerindicator:2.4.2'
    implementation 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
}

apply plugin: 'com.google.gms.google-services'